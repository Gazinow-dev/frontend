plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'com.facebook.react'
    id 'com.google.gms.google-services'
}

/**
 * React Native 설정 블록
 */
react {
}

/**
 * 빌드 관련 변수
 */
def enableProguardInReleaseBuilds = false
def jscFlavor = 'org.webkit:android-jsc:+'

// Sentry 설정
apply from: new File(["node", "--print", "require.resolve('@sentry/react-native/package.json')"].execute().text.trim(), "../sentry.gradle")

android {
    ndkVersion rootProject.ext.ndkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion
    compileSdk rootProject.ext.compileSdkVersion

    namespace "com.gazinow.now"

    defaultConfig {
        applicationId "com.gazinow.now"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 23
        versionName "1.3.0"
        externalNativeBuild {
            cmake {
                arguments "-DANDROID_ALIGNED_PAGESIZE=16384"
            }
        }
        ndk {
            abiFilters "armeabi-v7a", "arm64-v8a", "x86", "x86_64"
        }
    }
    
    packagingOptions {
        jniLibs {
            // AAB/APK 내부의 .so 파일들이 시스템에서 직접 읽힐 수 있도록(Uncompressed) 정렬
            useLegacyPackaging = false
        }
        // 16 KB 정렬을 보장
        resources {
            excludes += "/**/libc++_shared.so"
        }
    }
    
    // Java 17 컴파일 설정
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    // Kotlin JVM 타겟 설정
    kotlinOptions {
        jvmTarget = '17'
    }

    signingConfigs {
        debug {
            storeFile file('debug.keystore')
            storePassword 'android'
            keyAlias 'androiddebugkey'
            keyPassword 'android'
        }
        release {
            if (project.hasProperty('MYAPP_UPLOAD_STORE_FILE')) {
                storeFile file(MYAPP_UPLOAD_STORE_FILE)
                storePassword MYAPP_UPLOAD_STORE_PASSWORD
                keyAlias MYAPP_UPLOAD_KEY_ALIAS
                keyPassword MYAPP_UPLOAD_KEY_PASSWORD
            }
        }
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.debug
        }
        releaseStaging {
            initWith release
            matchingFallbacks = ['release']
        }
        release {
            signingConfig signingConfigs.release
            minifyEnabled enableProguardInReleaseBuilds
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"
        }
    }
}

dependencies {
    implementation("com.facebook.react:react-android")
    implementation("com.facebook.react:flipper-integration")

    implementation platform("com.google.firebase:firebase-bom:33.1.2")
    implementation("com.google.firebase:firebase-messaging")
    implementation("com.google.firebase:firebase-analytics")

    if (hermesEnabled.toBoolean()) {
        implementation("com.facebook.react:hermes-android")
    } else {
        implementation jscFlavor
    }

    implementation project(':react-native-splash-screen')
}

configurations.all {
    resolutionStrategy.eachDependency { details ->
        if (details.requested.group == 'androidx.browser') {
            details.useVersion '1.8.0'
        }
    }
}

// 모든 패키징 태스크에 대해 16KB 정렬 옵션을 강제 적용
tasks.withType(DefaultTask).configureEach { task ->
    if (task.name.startsWith("package")) {
        try {
            // jniLibs 정렬을 16KB(16384)로 설정하도록 힌트를 줌
            task.setProperty("extractNativeLibs", true)
        } catch (Exception e) {
        }
    }
}

apply from: file("../../node_modules/@react-native-community/cli-platform-android/native_modules.gradle");
applyNativeModulesAppBuildGradle(project)
